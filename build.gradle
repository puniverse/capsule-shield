buildscript {
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:5.0'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
    id 'maven'
    id 'signing'
    // id 'net.sf.proguard.proguard-gradle' version '5.0'
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

group               = "co.paralleluniverse"
version             = "0.2.0-SNAPSHOT"
status              = "integration"
description         = "Container Caplet"
ext.url             = "http://puniverse.github.com/quasar"
ext.vendor          = "Parallel Universe Software Co."
ext.licenseName     = "Eclipse Public License - v 1.0"
ext.licenseUrl      = "http://www.eclipse.org/legal/epl-v10.html"
ext.scmUrl          = "https://github.com/puniverse/${project.name}"
ext.scmConnection   = "https://github.com/puniverse/${project.name}.git"
ext.distDir         = "$buildDir/dist"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

ext.capsule = 'Capsule'
ext.caplet  = 'ShieldedCapsule'

ext.capsuleVer = '1.0.1-SNAPSHOT'
ext.log4jVer   = '1.2.17'
ext.slf4jVer   = '1.7.12'

if (!project.hasProperty("sonatypeUsername") || !project.hasProperty("sonatypePassword")) {
    println "sonatype username or password not set"
    ext.sonatypeUsername = ""
    ext.sonatypePassword = ""
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

configurations {
    [compile, runtime]*.resolutionStrategy {
        failOnVersionConflict()
    }
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
    compile "$group:capsule:$capsuleVer"
    compile "$group:capsule-util:$capsuleVer"
    compile "log4j:log4j:$log4jVer"

    provided "org.slf4j:jul-to-slf4j:$slf4jVer"
}

shadowJar {
    outputs.upToDateWhen { false }

    // artifactAttached = false
    classifier = '' // overwrite jar
    destinationDir = file("$buildDir/libs")

    exclude 'META-INF/**'
    exclude 'org/apache/log4j/xml/log4j.dtd'

    relocate 'org.', 'capsule.org.'

    manifest {
        attributes (
            'Premain-Class' : capsule,
            'Main-Class'    : capsule,
            'Caplets'       : caplet
        )
    }
}

task proguard(type: proguard.gradle.ProGuardTask, dependsOn:[shadowJar]) {
    injars jar
    outjars "$buildDir/libs/${project.name}-${version}-shrunk.jar"
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"

    keep "class Capsule { *; }"
    keep "class ${caplet} { *; }"
    keeppackagenames "co.paralleluniverse.capsule"

    dontoptimize
    dontobfuscate
    dontwarn

    doLast {
        logger.info("$buildDir/libs/${project.name}-${project.version}-shrunk.jar -> $buildDir/libs/${project.name}-${project.version}.jar")
        def out = file("$buildDir/libs/${project.name}-${project.version}.jar")
        if (out.exists())
            out.delete()
        file("$buildDir/libs/${project.name}-${version}-shrunk.jar").renameTo(out)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

///////// Publish Artifacts
apply plugin: 'maven'
apply plugin: 'signing'

artifacts {
    archives jar
    archives sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(
                url: (isReleaseVersion ?
                        "https://oss.sonatype.org/service/local/staging/deploy/maven2" :
                        "https://oss.sonatype.org/content/repositories/snapshots")) {
                // User and Password are taken from ~/.gradle/gradle.properties
                authentication(userName: project.sonatypeUsername, password: project.sonatypePassword)
            }
            pom.project {
                name project.name
                packaging 'jar'
                description project.description
                url project.url
                scm {
                    url project.scmUrl
                    connection project.scmConnection
                    developerConnection project.scmConnection
                }
                licenses {
                    license {
                        name project.licenseName
                        url project.licenseUrl
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'pron'
                        name 'Ron Pressler'
                    }
                    developer {
                        id 'circlespainter'
                        name 'Fabio Tudone'
                    }
                }
            }
        }
    }
}

[assemble, build, install, signArchives, uploadArchives]*.dependsOn proguard

install.repositories.mavenInstaller {
    pom.whenConfigured {
        it.dependencies.clear()
    }
}

uploadArchives.repositories.mavenDeployer {
    pom.whenConfigured {
        it.dependencies.clear()
    }
}

defaultTasks 'build'
